#!/usr/bin/env coffee

log  = require './lib/log'
path = require 'path'

(cmd = require 'commander')
  .version('0.0.1')
  .option('-p, --port [PORT]', 'Port to listen on', 8000)
  .parse(process.argv)

building = false
rebuild = (cb) ->
  unless building
    building = true
    build = (require 'child_process').spawn "#{__dirname}/fetool", ['build', '--render-drafts'],
      stdio: 'inherit'

    build.on 'close', (code) ->
      building = false
      cb?()

process.stdin.resume()
process.stdin.on 'data', -> rebuild()

rebuild ->
  connect = require 'connect'
  connect()
    .use(connect.static('./public'))
    .listen(cmd.port)

  log "Dev server listening on port #{cmd.port}"
  log "Press return to rebuild"

  (require 'gaze') "#{process.cwd()}/fe/**", (err, watcher) ->
    @on 'all', -> rebuild()

